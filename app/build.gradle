apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

ext {
    versions = [
            kotlin          : '1.2.41',
            android         : [
                    support       : '27.1.1',
                    constraint    : '1.1.0-beta5',
                    architecture  : '1.1.1',
            ],
            dagger          : '2.11',
            butterknife     : '8.8.1',
            rxjava          : '2.0.2',
            rxandroid       : '2.0.1',
            stream          : '1.1.9',
            autovalue       : [
                    main   : '1.5',
                    parcel : '0.2.6',
                    ignore : '1.1.4',
            ],
            glide           : '4.1.1',
            adapterdelegates: '3.0.1',
            testing         : [
                    junit   : '4.12',
                    mockito : '2.5.5',
                    espresso: '2.2.2',
            ],
    ]
    app = [
            android    : [
                    compileSdkVersion : 27,
                    minSdkVersion     : 15,
                    buildToolsVersion : '27.0.3',
            ],
            versionCode: 1,
            versionName: '1.0',
    ]
}

android {
    compileSdkVersion app.android.compileSdkVersion
    buildToolsVersion app.android.buildToolsVersion

    defaultConfig {
        applicationId "com.lucasurbas.counter"
        minSdkVersion app.android.minSdkVersion
        targetSdkVersion app.android.compileSdkVersion
        versionCode app.versionCode
        versionName app.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"

    // Android Support
    implementation "com.android.support:appcompat-v7:$versions.android.support"
    implementation "com.android.support:support-v4:$versions.android.support"
    implementation "com.android.support:cardview-v7:$versions.android.support"
    implementation "com.android.support:design:$versions.android.support"
    implementation "com.android.support.constraint:constraint-layout:$versions.android.constraint"
    implementation "com.android.support:support-annotations:$versions.android.support"

    // Android Architecture
    implementation "android.arch.lifecycle:runtime:$versions.android.architecture"
    implementation "android.arch.lifecycle:extensions:$versions.android.architecture"
    kapt "android.arch.lifecycle:compiler:$versions.android.architecture"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    // Butterknife
    implementation "com.jakewharton:butterknife:$versions.butterknife"
    kapt "com.jakewharton:butterknife-compiler:$versions.butterknife"

    // AutoValue
    compileOnly "com.jakewharton.auto.value:auto-value-annotations:$versions.autovalue.main"
    kapt "com.google.auto.value:auto-value:$versions.autovalue.main"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
    implementation "com.annimon:stream:$versions.stream"

    // Adapter Delegates
    implementation "com.hannesdorfmann:adapterdelegates3:$versions.adapterdelegates"

    // Test
    testImplementation "junit:junit:$versions.testing.junit"
    testImplementation "org.mockito:mockito-core:$versions.testing.mockito"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.testing.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

kapt.generateStubs = true
